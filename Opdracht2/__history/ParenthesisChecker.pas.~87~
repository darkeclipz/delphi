unit ParenthesisChecker;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.StdCtrls, Stack;

type
  TParenthesisCheckerForm = class(TForm)
    EditInput: TEdit;
    Label1: TLabel;
    ButtonTryCheck: TButton;
    ButtonEmpyNotifications: TButton;
    ButtonEmptyForm: TButton;
    MemoOutput: TMemo;
    procedure ButtonTryCheckClick(Sender: TObject);
    procedure ButtonEmptyFormClick(Sender: TObject);
    procedure ButtonEmpyNotificationsClick(Sender: TObject);
  private
    { Private declarations }
    procedure ClearOutput;
    procedure ClearForm;
    procedure AddLineToOutput(Text: String);
    procedure CheckParenthesis;
    function IsParenthesis(Character: Char): Boolean;
    function IsOpeningParenthesis(Character: Char): Boolean;
    function IsClosingParenthesis(Character: Char): Boolean;
    function IsMatch(Left: Char; Right: Char): Boolean;
  public
    { Public declarations }
  end;

var
  ParenthesisCheckerForm: TParenthesisCheckerForm;

implementation

{$R *.dfm}

procedure TParenthesisCheckerForm.ClearOutput;
begin
  MemoOutput.Lines.Clear;
end;

procedure TParenthesisCheckerForm.ButtonEmptyFormClick(Sender: TObject);
begin
  ClearForm;
end;

procedure TParenthesisCheckerForm.ButtonEmpyNotificationsClick(Sender: TObject);
begin
  ClearOutput;
end;

procedure TParenthesisCheckerForm.ButtonTryCheckClick(Sender: TObject);
begin
  try
    ClearOutput;
    CheckParenthesis;
  except
    on E: Exception do
      AddLineToOutput(E.Message);
  end;
end;

procedure TParenthesisCheckerForm.CheckParenthesis;
var
  i: Integer;
  CurrentChar: Char;
  Stack: TStack;
  IsInputCorrect: Boolean;
begin
  IsInputCorrect := True;
  Stack := TStack.Create;
  Stack.Instantiate;

  for i := 1 to Length(EditInput.Text) do { String indexing is 1-based }
  begin
    CurrentChar := EditInput.Text[i];

    if not IsParenthesis(CurrentChar) then
      continue;

    if Stack.IsEmpty and IsClosingParenthesis(CurrentChar) then
    begin
      IsInputCorrect := False;
      AddLineToOutput('Sluithaakje teveel voor ' + CurrentChar + ' op positie '
        + IntToStr(i) + '.');
      continue;
    end;

    if IsOpeningParenthesis(CurrentChar) then
      Stack.Push(CurrentChar);

    if IsClosingParenthesis(CurrentChar) then
    begin
      if IsMatch(Stack.Peek, CurrentChar) then
      begin
        Stack.Pop;
      end
      else
      begin
        IsInputCorrect := False;
        AddLineToOutput('Foutief sluithaakje ' + CurrentChar + ' op positie '
          + IntToStr(i) + '.');
      end;
    end;
  end;

  while not Stack.IsEmpty do
  begin
    IsInputCorrect := False;
    AddLineToOutput('Geen sluithaakje voor ' + Stack.Pop + '.');
  end;

  if IsInputCorrect then
    AddLineToOutput('Alle haakjes zijn correct gepaard.');
end;

procedure TParenthesisCheckerForm.ClearForm;
begin
  ClearOutput;
  EditInput.Clear;
  EditInput.SetFocus;
end;

procedure TParenthesisCheckerForm.AddLineToOutput(Text: String);
begin
  MemoOutput.Lines.Add(Text);
  SendMessage(MemoOutput.Handle, WM_VSCROLL, SB_LINEDOWN, 0); { Scroll to end. }
end;

function TParenthesisCheckerForm.IsParenthesis(Character: Char): Boolean;
begin
  Result := IsOpeningParenthesis(Character) or IsClosingParenthesis(Character);
end;

function TParenthesisCheckerForm.IsOpeningParenthesis(Character: Char): Boolean;
begin
  Result := (Character = '[') or (Character = '(') or (Character = '{');
end;

function TParenthesisCheckerForm.IsClosingParenthesis(Character: Char): Boolean;
begin
  Result := (Character = ']') or (Character = ')') or (Character = '}');
end;

function TParenthesisCheckerForm.IsMatch(Left: Char; Right: Char): Boolean;
begin
  Result := (Left = '[') and (Right = ']')
         or (Left = '(') and (Right = ')')
         or (Left = '{') and (Right = '}')
end;

end.
